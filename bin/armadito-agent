#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars) ;
use Getopt::Long;
use Pod::Usage;
#use Data::Dumper;

use Armadito::Agent;
use Armadito::Agent::Task;
use Armadito::Agent::Task::State;
use Armadito::Agent::Task::State::Armadito;
use Armadito::Agent::Task::Getjobs;
use Armadito::Agent::Task::Getjobs::Armadito;
use Armadito::Agent::Task::Enrollment;
use Armadito::Agent::Task::Enrollment::Armadito;

my $options = {};

pod2usage(-verbose => 0, -exitstatus => 0) unless $ARGV[0];

GetOptions(
    $options,
	'server|s=s',
	'task|t=s',
	'antivirus|av=s',
	'conf-fusion|cf=s',
	'conf-armadito|ca=s',
	'conf-fusion-file|cff=s',
	'conf-armadito-file|caf=s',
	'list-tasks',
	'list-avs',
	'help|h',
	'version|v',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitstatus => 0) if $options->{help};

if ($options->{version}) {
    print "armadito-agent $Armadito::Agent::Task::VERSION\n";
    exit 0;
}

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

if((!defined($options->{'conf-fusion-file'}) 
 && defined($options->{'conf-fusion'}) 
 && $options->{'conf-fusion'} ne 'file' 
 && $options->{'conf-fusion'} ne 'registry') 
  || (!defined($options->{'conf-fusion'}) 
   && !defined($options->{'conf-fusion-file'}))
 ){
	print "Please install Fusion Inventory and provide --conf-fusion mode (registry or file) or --conf-fusion-file file.\n";
	print "Armadito Agent needs Fusion Inventory configuration to work correctly.\n";
	print "See usage : \n\n";
	pod2usage(-verbose => 0, -exitstatus => 1);
}

if(defined($options->{'conf-fusion'})	
 && $options->{'conf-fusion'} eq 'file' 
 && !$options->{'conf-fusion-file'}){
	print "Argument --conf-fusion-file|cff <FILE> is mandatory if you use --conf-fusion|cf file.\n";
	print "See usage for more information. \n\n";
	pod2usage(-verbose => 0, -exitstatus => 1);
}

my $agent = Armadito::Agent->new(%setup);

if ($options->{'list-tasks'}) {
   $agent->displaySupportedTasks();
   exit 0;
}

if ($options->{'list-avs'}) {
   $agent->displaySupportedAVs();
   exit 0;
}

## default
$options->{antivirus} = "Armadito" unless(defined($options->{antivirus}));
$options->{server} = "http://armadito-glpi/glpi/plugins/armadito/" unless(defined($options->{server}));

my $config = {
    plugin_server_url => $options->{server},
    av_server_url => "http://localhost/armaditoAV:8081"
};

eval{
	# validate args
	$agent->isAVSupported($options->{antivirus}) or die "Unsupported Antivirus. Use --list-avs to see which antiviruses are supported.";
	$agent->isTaskSupported($options->{task}) or die "Unsupported Task. Use --list-tasks to see which tasks are supported.";

	## init
	$agent->init(options => $options);

	print "Fusion Device Id : ".$agent->{fusionid}.".\n";
	#print Dumper($agent->{config})."\n";

	## new task instance
	my $task = "Armadito::Agent::Task::$options->{task}::$options->{antivirus}"->new(config => $config, agent => $agent);

	## run task
	$task->run();
};

if ($EVAL_ERROR) {
    print STDERR "Execution failure:\n";
    print STDERR $EVAL_ERROR;
    exit(1);
}

exit(0);
__END__

=head1 NAME

armadito-agent - command line interface script used for Armadito Agent.

=head1 SYNOPSIS

armadito-agent --server <server> --task <task>

  Options:
    --help                 this menu

  Target definition options:
    --server server        Armadito Plugin for GLPI server URL

  Task selection options:
    --task task            Task to be executed
	--list-tasks           List supported tasks

  Antivirus selection options:
    --antivirus antivirus  Antivirus to be managed
	--list-avs             List supported antiviruses

  Configuration options:
	--conf-armadito=mode             armadito configuration mode (registry or file)
	--conf-fusion=mode               fusion inventory configuration mode (registry or file)
	--conf-fusion-file=FILE          fusion inventory configuration file path
	--conf-armadito-file=FILE        armadito configuration file path

=head1 EXAMPLES

	% armadito-agent --task "Enrollment" --conf-fusion file --conf-fusion-file /etc/fusioninventory/agent.cfg
		eq
	% armadito-agent -t "Enrollment" -cf file -cff /etc/fusioninventory/agent.cfg
		eq
	% armadito-agent -t "Enrollment" -cff /etc/fusioninventory/agent.cfg

=head1 DESCRIPTION

F<armadito-agent> is the command line interface for Armadito Agent.

=head1 OPTIONS

Some options are available in a I<short> form and a I<long> form.  For
example, the two lines below are all equivalent:

    % armadito-agent -s localhost
    % armadito-agent --server localhost

=head2 Target definition options

=over

=item B<-s>, B<--server>=I<URI>

Send the results of tasks execution to given server.

In general, Armadito plugin URLs are like the following:
    http://servername/glpi/plugins/armadito/index.php

=back

=head2 Task selection options

=over

=item B<--list-tasks>

List all available tasks and exit

=item B<--task>=I<TASK>

Run given task immediately.

See option I<--list-tasks> for the list of available tasks.

=back

=head2 Antivirus selection options

=over

=item B<--list-avs>

List all available antiviruses and exit

=item B<--antivirus>=I<ANTIVIRUS>

Mange the given Antivirus

See option I<--list-avs> for the list of available antiviruses.

=head2 Configuration options

=over

=item B<--conf-fusion>=I<MODE>

Configuration mode used by already installed fusion inventory.

The available modes are:

=over 4

=item

file: read configuration from a file (default anywhere else as Windows).

=item

registry: read configuration from the registry (default on Windows).

=item

none: don't read any configuration.

=back

=item B<--conf-armadito>=I<MODE>

Configuration mode to use for armadito agent.

The available modes are:

=over 4

=item

file: read configuration from a file (default anywhere else as Windows).

=item

registry: read configuration from the registry (default on Windows).

=item

none: don't read any configuration.

=back

=item B<--conf-file-fusion>=I<FILE>

Use I<FILE> as fusion inventory configuration file (implies file configuration mode).

=back

=item B<--conf-file-armadito>=I<FILE>

Use I<FILE> as armadito configuration file (implies file configuration mode).

=back
